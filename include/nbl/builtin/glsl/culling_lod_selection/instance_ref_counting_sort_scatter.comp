#version 440 core
layout(local_size_x=_NBL_GLSL_WORKGROUP_SIZE_) in;


#define NBL_GLSL_CULLING_LOD_SELECTION_INSTANCE_LIST_DESCRIPTOR_BINDING
#define NBL_GLSL_CULLING_LOD_SELECTION_INSTANCE_LOD_INFO_UVEC4_OFFSETS_DESCRIPTOR_BINDING
#define NBL_GLSL_CULLING_LOD_SELECTION_INSTANCE_DRAWCALL_INCLUSIVE_COUNTS_DESCRIPTOR_BINDING
#define NBL_GLSL_CULLING_LOD_SELECTION_PVS_INSTANCE_DRAWS_DESCRIPTOR_QUALIFIERS restrict readonly
// dont pull the scan scratch descriptor (we'll use our own decl for clearing)
#define _NBL_GLSL_SCAN_DESCRIPTORS_INCLUDED_
#include <nbl/builtin/glsl/culling_lod_selection/input_descriptor_set.glsl>

#define NBL_GLSL_CULLING_LOD_SELECTION_DRAW_CALLS_DESCRIPTOR_QUALIFIERS restrict readonly
#define NBL_GLSL_CULLING_LOD_SELECTION_PER_VIEW_PER_INSTANCE_DESCRIPTOR_BINDING
#define NBL_GLSL_CULLING_LOD_SELECTION_DRAW_CALL_COUNTS_DESCRIPTOR_BINDING
#include <nbl/builtin/glsl/culling_lod_selection/output_descriptor_set.glsl>


uint nbl_glsl_culling_lod_selection_getSortedDrawInstanceIndex(in uint unsortedDrawInstanceID)
{
    const uvec2 data = pvsInstanceDraws.data[unsortedDrawInstanceID].xy;
    const uint drawBaseInstanceDWORDOffset = data[0];
    return nbl_glsl_culling_lod_selection_drawCallGetDWORD(drawBaseInstanceDWORDOffset)+data[1];
}
uvec2 nbl_glsl_culling_lod_selection_getDrawInstanceAttributes(in uint unsortedDrawInstanceID)
{
    return pvsInstanceDraws.data[unsortedDrawInstanceID].zw;
}

layout(set=_NBL_GLSL_SCAN_DESCRIPTOR_SET_DEFINED_,binding=_NBL_GLSL_SCAN_SCRATCH_BINDING_DEFINED_) restrict writeonly buffer ScanScratchBuffer
{
    uint data[];
} scanScratch;
#include <nbl/builtin/glsl/scan/parameters_struct.glsl>
layout(push_constant) uniform PushConstants
{
	nbl_glsl_scan_Parameters_t scanParams;
} pc;

void main()
{
    if (gl_GlobalInvocationID.x==0u)
        dispatchIndirect.instanceDrawCull.num_groups_x = 0u;

    const uint lastScratchDWORDToClear = pc.scanParams.temporaryStorageOffset[0];
    for (uint dword=gl_GlobalInvocationID.x; dword<=lastScratchDWORDToClear; dword+=_NBL_GLSL_WORKGROUP_SIZE_)
        scanScratch.data[dword] = 0u;

    const uint potentiallyVisibleInstanceCount = pvsInstanceDraws.count;
    for (uint unsortedDrawInstanceID=gl_GlobalInvocationID.x; unsortedDrawInstanceID<potentiallyVisibleInstanceCount; unsortedDrawInstanceID+=_NBL_GLSL_WORKGROUP_SIZE_)
    {
        const uint instanceIndex = nbl_glsl_culling_lod_selection_getSortedDrawInstanceIndex(unsortedDrawInstanceID);
        perInstanceRedirectAttrs.data[instanceIndex] = nbl_glsl_culling_lod_selection_getDrawInstanceAttributes(unsortedDrawInstanceID);
    }
}